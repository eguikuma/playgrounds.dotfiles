function gish_help
    echo "ğŸŒ± gish - Gitãƒ¦ãƒ¼ãƒ†ã‚£ãƒªãƒ†ã‚£

[ä½¿ç”¨æ–¹æ³•]
gish [ã‚µãƒ–ã‚³ãƒãƒ³ãƒ‰:REQUIRED] [ã‚ªãƒ—ã‚·ãƒ§ãƒ³]

[èª¬æ˜]
Gitæ“ä½œã‚’è£œåŠ©ã™ã‚‹ãŸã‚ã®ãƒ¦ãƒ¼ãƒ†ã‚£ãƒªãƒ†ã‚£ãƒ„ãƒ¼ãƒ«ã§ã™ã€‚

[ã‚µãƒ–ã‚³ãƒãƒ³ãƒ‰]
fix-commit          ã‚³ãƒŸãƒƒãƒˆã®æ—¥æ™‚ãƒ»ä½œæˆè€…æƒ…å ±ã‚’ä¿®æ­£
help                ãƒ˜ãƒ«ãƒ—ã‚’è¡¨ç¤º

[ã‚ªãƒ—ã‚·ãƒ§ãƒ³]
-h, --help   ã“ã®ãƒ˜ãƒ«ãƒ—ã‚’è¡¨ç¤º

[ä¾‹]
gish fix-commit -d
gish fix-commit -u
gish help fix-commit"
end

function _gish_fix_commit_help
    echo "ğŸŒ± gish fix-commit - ã‚³ãƒŸãƒƒãƒˆä¿®æ­£

[ä½¿ç”¨æ–¹æ³•]
gish fix-commit [ã‚ªãƒ—ã‚·ãƒ§ãƒ³]

[èª¬æ˜]
Gitã‚³ãƒŸãƒƒãƒˆã®æ—¥æ™‚ã‚„ä½œæˆè€…æƒ…å ±ã‚’ä¿®æ­£ã—ã¾ã™ã€‚
ã‚³ãƒŸãƒƒãƒˆã‚’æŒ‡å®šã—ãªã„å ´åˆã¯ã€æ´¾ç”Ÿå…ƒã‚³ãƒŸãƒƒãƒˆã‚’è‡ªå‹•æ¤œå‡ºã—ã¾ã™ã€‚

[ã‚ªãƒ—ã‚·ãƒ§ãƒ³]
-d, --date [<datetime>]   æ—¥æ™‚ä¿®æ­£ï¼ˆå¼•æ•°ãªã—=åŒæœŸã€å¼•æ•°ã‚ã‚Š=å›ºå®šæ—¥æ™‚ï¼‰
-u, --user                ä½œæˆè€…æƒ…å ±ã®ä¿®æ­£ï¼ˆå¯¾è©±å¼ã§é¸æŠï¼‰
-b, --base <ref>          æ¯”è¼ƒå…ƒã®ãƒ–ãƒ©ãƒ³ãƒã‚’æŒ‡å®šï¼ˆãƒ‡ãƒ•ã‚©ãƒ«ãƒˆï¼šmain/masterï¼‰
-r, --root                æŒ‡å®šã—ãŸã‚³ãƒŸãƒƒãƒˆè‡ªä½“ã‚‚å‡¦ç†å¯¾è±¡ã«å«ã‚ã‚‹
-y, --yes                 ç¢ºèªãƒ—ãƒ­ãƒ³ãƒ—ãƒˆã‚’ã‚¹ã‚­ãƒƒãƒ—
-h, --help                ã“ã®ãƒ˜ãƒ«ãƒ—ã‚’è¡¨ç¤º

[æ—¥æ™‚å½¢å¼]
-d ã‚ªãƒ—ã‚·ãƒ§ãƒ³ã§æŒ‡å®šå¯èƒ½ãªå½¢å¼ï¼š
  2025-09-01                           å¹´æœˆæ—¥ã®ã¿
  2025-09-01 12:00:00                  å¹´æœˆæ—¥ï¼‹æ™‚åˆ»
  'Mon Sep 01 12:00:00 2025 +0900'     Gitå½¢å¼ï¼ˆã‚¿ã‚¤ãƒ ã‚¾ãƒ¼ãƒ³ä»˜ãï¼‰

[ä¾‹]
gish fix-commit -d                 CommitterDateã‚’AuthorDateã«åŒæœŸ
gish fix-commit -d '2025-09-01'    æŒ‡å®šæ—¥æ™‚ã«çµ±ä¸€
gish fix-commit -u                 ä½œæˆè€…æƒ…å ±ã‚’å¯¾è©±å¼ã§ä¿®æ­£
gish fix-commit -d -u abc123       æ—¥æ™‚ã¨ä½œæˆè€…ã‚’åŒæ™‚ã«ä¿®æ­£
gish fix-commit -r abc123          æŒ‡å®šã‚³ãƒŸãƒƒãƒˆè‡ªä½“ã‚‚å«ã‚ã¦ä¿®æ­£
gish fix-commit -d -u -y           ç¢ºèªãªã—ã§æ—¥æ™‚ãƒ»ä½œæˆè€…ã‚’ä¿®æ­£"
end

function gish -d Gitä¿®æ­£ãƒ„ãƒ¼ãƒ«
    if test (count $argv) -eq 0
        gish_help
        return 0
    end

    set -l subcommand $argv[1]

    switch $subcommand
        case fix-commit
            set -l fix_commit_args $argv[2..-1]
            _gish_fix_commit $fix_commit_args
            return $status
        case help
            if test (count $argv) -eq 2 && test "$argv[2]" = fix-commit
                _gish_fix_commit_help
            else
                gish_help
            end
            return 0
        case -h --help
            gish_help
            return 0
        case '*'
            gish_help
            return 1
    end
end

function _gish_normalize_date
    set -l input_date $argv[1]

    # å¼•æ•°ãªã—ã®å ´åˆã¯ç¾åœ¨æ—¥æ™‚
    if test -z "$input_date"
        env LC_TIME=C date "+%a %b %d %H:%M:%S %Y %z"
        return 0
    end

    # æ—¢ã«Gitå½¢å¼ï¼ˆä¾‹ï¼šMon Sep 01...ï¼‰ã®å ´åˆã¯ãã®ã¾ã¾
    if string match -qr '^[A-Z][a-z][a-z] [A-Z][a-z][a-z] [0-9]' $input_date
        echo $input_date
        return 0
    end

    # ISOå½¢å¼ï¼ˆYYYY-MM-DDï¼‰ã®å ´åˆã¯Gitå½¢å¼ã«å¤‰æ›
    if string match -qr '^[0-9]{4}-[0-9]{1,2}-[0-9]{1,2}' $input_date
        # æ™‚åˆ»ãŒãªã„å ´åˆã¯00:00:00ã‚’è¿½åŠ ã—ã¾ã™
        if not string match -qr '[0-9]{2}:[0-9]{2}' $input_date
            set input_date "$input_date 00:00:00"
        end

        # ã‚¿ã‚¤ãƒ ã‚¾ãƒ¼ãƒ³ãŒãªã„å ´åˆã¯ã‚·ã‚¹ãƒ†ãƒ ã®ã‚¿ã‚¤ãƒ ã‚¾ãƒ¼ãƒ³ã‚’è¿½åŠ 
        if not string match -qr '[+-][0-9]{4}' $input_date
            set -l tz (date "+%z")
            set input_date "$input_date $tz"
        end

        if set -l git_date (env LC_TIME=C date -d "$input_date" "+%a %b %d %H:%M:%S %Y %z" 2>/dev/null)
            echo $git_date
            return 0
        end
    end

    # ãã®ä»–ã®å½¢å¼ã¯dateã‚³ãƒãƒ³ãƒ‰ã§å¤‰æ›
    if set -l git_date (env LC_TIME=C date -d "$input_date" "+%a %b %d %H:%M:%S %Y %z" 2>/dev/null)
        echo $git_date
        return 0
    end

    set_color red >&2
    echo "â›” æŒ‡å®šã•ã‚ŒãŸæ—¥æ™‚å½¢å¼ã¯æ­£ã—ãã‚ã‚Šã¾ã›ã‚“" >&2
    set_color normal >&2
    return 1
end

function _gish_validate_git_repo
    if not git rev-parse --git-dir >/dev/null 2>&1
        set_color red
        echo "â›” ã‚«ãƒ¬ãƒ³ãƒˆãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªã¯Gitãƒªãƒã‚¸ãƒˆãƒªã§ã¯ã‚ã‚Šã¾ã›ã‚“"
        set_color normal
        return 1
    end
    return 0
end

function _gish_validate_commit
    set -l commit $argv[1]
    if not git rev-parse --verify $commit >/dev/null 2>&1
        set_color red
        echo "â›” æŒ‡å®šã•ã‚ŒãŸã‚³ãƒŸãƒƒãƒˆã¯å­˜åœ¨ã—ã¾ã›ã‚“"
        set_color normal
        return 1
    end
    return 0
end

function _gish_find_default_branch
    if git show-ref --verify --quiet refs/heads/main
        echo main
    else if git show-ref --verify --quiet refs/heads/master
        echo master
    else
        return 1
    end
    return 0
end

function _gish_detect_base_commit
    set -l base_branch $argv[1]

    # åˆ†å²å…ƒã‚³ãƒŸãƒƒãƒˆã‚’æ¤œå‡º
    set -l commit_hash (git show-branch --sha1-name 2>/dev/null | command grep '*' | command grep -v (git rev-parse --abbrev-ref HEAD) | command grep '+' | head -1 | awk -F'[]~^[]' '{print $2}' 2>/dev/null)
    if test -z "$commit_hash"
        set commit_hash (git merge-base $base_branch (git rev-parse --abbrev-ref HEAD) 2>/dev/null)

        # merge-baseãŒHEADã¨åŒã˜å ´åˆã¯ãƒ«ãƒ¼ãƒˆã‚³ãƒŸãƒƒãƒˆã‚’ä½¿ç”¨
        if test "$commit_hash" = (git rev-parse HEAD 2>/dev/null)
            set commit_hash (git rev-list --max-parents=0 HEAD 2>/dev/null)
        end
    end

    if test -n "$commit_hash"
        echo $commit_hash
        return 0
    end

    return 1
end

function _gish_fix_commit
    argparse h/help d/date u/user 'b/base=' r/root y/yes -- $argv
    or return 1

    if set -q _flag_help
        _gish_fix_commit_help
        return 0
    end

    set -l fix_commit_date false
    set -l date_str ""
    set -l fix_commit_user false
    set -l base_branch ""
    set -l include_root false
    set -l skip_confirm false
    set -l remaining_args

    if set -q _flag_date
        set fix_commit_date true
        # $argv ã®æœ€åˆã®è¦ç´ ãŒæ—¥ä»˜å½¢å¼ã®å ´åˆã€date_str ã¨ã—ã¦ä½¿ç”¨
        if test (count $argv) -ge 1
            set -l first_arg $argv[1]
            # "-" ã§å§‹ã¾ã‚‰ãšã€æ•°å­—ã‚’å«ã‚€å ´åˆã¯æ—¥ä»˜
            if not string match -q -- '-*' $first_arg
                if string match -qr '[0-9]' $first_arg
                    set date_str $first_arg
                    set argv $argv[2..-1]
                end
            end
        end
    end

    if set -q _flag_user
        set fix_commit_user true
    end

    if set -q _flag_base
        set base_branch $_flag_base
    end

    if set -q _flag_root
        set include_root true
    end

    if set -q _flag_yes
        set skip_confirm true
    end

    # æ®‹ã‚Šã®å¼•æ•°ã‚’ã‚»ãƒƒãƒˆ
    set remaining_args $argv

    _gish_validate_git_repo
    or return 1

    if test $fix_commit_date != true && test $fix_commit_user != true
        _gish_fix_commit_help
        return 1
    end

    set -l normalized_date ""
    if test $fix_commit_date = true && test -n "$date_str"
        set normalized_date (_gish_normalize_date "$date_str")
        if test $status -ne 0
            return 1
        end
    end

    if test -z "$base_branch"
        set base_branch (_gish_find_default_branch)
        if test $status -ne 0
            set_color red
            echo "â›” mainã‚‚ã—ãã¯masterãƒ–ãƒ©ãƒ³ãƒãŒè¦‹ã¤ã‹ã‚Šã¾ã›ã‚“"
            set_color normal
            return 1
        end
    end

    set -l target_commit
    if test (count $remaining_args) -ge 1
        set target_commit $remaining_args[1]
        _gish_validate_commit $target_commit
        or return 1
    else
        set target_commit (_gish_detect_base_commit $base_branch)
        if test $status -ne 0 || test -z "$target_commit"
            set_color red
            echo "â›” æ´¾ç”Ÿå…ƒã‚³ãƒŸãƒƒãƒˆã‚’æ¤œå‡ºã§ãã¾ã›ã‚“"
            set_color normal
            return 1
        end
    end

    _gish_execute_fix_commit "$target_commit" $fix_commit_date "$normalized_date" $fix_commit_user $include_root $skip_confirm
    return $status
end

function _gish_collect_user_info
    set -l commit_range $argv[1]

    # å‡ºç¾å›æ•°ã§ã‚«ã‚¦ãƒ³ãƒˆ
    set -l authors (git log --format="%an|%ae" $commit_range 2>/dev/null | sort | uniq -c | sort -nr)
    set -l committers (git log --format="%cn|%ce" $commit_range 2>/dev/null | sort | uniq -c | sort -nr)

    # é‡è¤‡ã‚’é™¤å»
    set -l all_users
    for line in $authors
        set -l count (echo $line | awk '{print $1}')
        set -l user (echo $line | awk '{$1=""; print $0}' | string trim)
        set all_users $all_users "author:$count:$user"
    end

    for line in $committers
        set -l count (echo $line | awk '{print $1}')
        set -l user (echo $line | awk '{$1=""; print $0}' | string trim)
        if not contains "author:$count:$user" $all_users
            set all_users $all_users "committer:$count:$user"
        end
    end

    for user in $all_users
        echo $user
    end
end

function _gish_interactive_user_selection
    set -l commit_range $argv[1]

    echo "1) Committerã‚’Authorã«åŒæœŸ" >&2
    echo "2) æ—¢å­˜ã®ãƒ¦ãƒ¼ã‚¶ãƒ¼æƒ…å ±" >&2
    echo "3) æ–°ã—ã„ãƒ¦ãƒ¼ã‚¶ãƒ¼æƒ…å ±" >&2

    read -l -P "é¸æŠ (1-3) > " selection

    switch $selection
        case 1
            echo "mode:sync"
            return 0
        case 2
            set -l user_info (_gish_collect_user_info $commit_range)
            if test (count $user_info) -eq 0
                set_color red >&2
                echo "â›” ç¯„å›²å†…ã«ãƒ¦ãƒ¼ã‚¶ãƒ¼æƒ…å ±ãŒè¦‹ã¤ã‹ã‚Šã¾ã›ã‚“" >&2
                set_color normal >&2
                return 1
            end

            echo >&2
            set -l i 1
            for info in $user_info
                set -l count (echo $info | cut -d: -f2)
                set -l user (echo $info | cut -d: -f3)
                echo "$i) $user ($count commits)" >&2
                set i (math $i + 1)
            end

            set -l git_name (git config user.name 2>/dev/null)
            set -l git_email (git config user.email 2>/dev/null)
            set -l git_config_index -1
            if test -n "$git_name" && test -n "$git_email"
                set git_config_index $i
                echo "$i) [git config] $git_name <$git_email>" >&2
                set i (math $i + 1)
            end

            read -l -P "é¸æŠ (1-"(math $i - 1)") > " user_selection

            if not string match -qr '^[0-9]+$' -- $user_selection
                set_color red >&2
                echo "â›” é¸æŠã•ã‚ŒãŸç•ªå·ã¯å­˜åœ¨ã—ã¾ã›ã‚“" >&2
                set_color normal >&2
                return 1
            end

            if test $user_selection -le (count $user_info)
                set -l selected_info $user_info[$user_selection]
                set -l user (echo $selected_info | cut -d: -f3)
                set -l name (echo $user | cut -d'|' -f1)
                set -l email (echo $user | cut -d'|' -f2)
                echo "mode:existing"
                echo "name:$name"
                echo "email:$email"
            else if test $git_config_index -ne -1 && test $user_selection -eq $git_config_index
                echo "mode:existing"
                echo "name:$git_name"
                echo "email:$git_email"
            else
                set_color red >&2
                echo "â›” é¸æŠã•ã‚ŒãŸç•ªå·ã¯å­˜åœ¨ã—ã¾ã›ã‚“" >&2
                set_color normal >&2
                return 1
            end
            return 0
        case 3
            echo >&2
            read -l -P "åå‰ > " new_name
            read -l -P "ãƒ¡ãƒ¼ãƒ« > " new_email

            if test -z "$new_name" || test -z "$new_email"
                set_color red >&2
                echo "â›” åå‰ã¾ãŸã¯ãƒ¡ãƒ¼ãƒ«ã‚¢ãƒ‰ãƒ¬ã‚¹ãŒå…¥åŠ›ã•ã‚Œã¦ã„ã¾ã›ã‚“" >&2
                set_color normal >&2
                return 1
            end

            echo "mode:new"
            echo "name:$new_name"
            echo "email:$new_email"
            return 0
        case '*'
            set_color red >&2
            echo "â›” é¸æŠã•ã‚ŒãŸç•ªå·ã¯å­˜åœ¨ã—ã¾ã›ã‚“" >&2
            set_color normal >&2
            return 1
    end
end

function _gish_select_target_user
    echo "1) Authorã®ã¿" >&2
    echo "2) Committerã®ã¿" >&2
    echo "3) Authorã¨Committerä¸¡æ–¹" >&2

    read -l -P "é¸æŠ (1-3) > " target_selection

    switch $target_selection
        case 1
            echo "target:author"
        case 2
            echo "target:committer"
        case 3
            echo "target:both"
        case '*'
            set_color red >&2
            echo "â›” é¸æŠã•ã‚ŒãŸç•ªå·ã¯å­˜åœ¨ã—ã¾ã›ã‚“" >&2
            set_color normal >&2
            return 1
    end
    return 0
end

function _gish_execute_fix_commit
    set -l target_commit $argv[1]
    set -l fix_commit_date $argv[2]
    set -l date_str $argv[3]
    set -l fix_commit_user $argv[4]
    set -l include_root $argv[5]
    set -l skip_confirm $argv[6]

    # ã‚³ãƒŸãƒƒãƒˆç¯„å›²ã‚’æ±ºå®š
    # --rootã‚ªãƒ—ã‚·ãƒ§ãƒ³ã®å ´åˆã¯å¯¾è±¡ã‚³ãƒŸãƒƒãƒˆè‡ªä½“ã‚‚å«ã‚ã‚‹
    set -l commit_range
    if test $include_root = true
        set -l root_commit (git rev-list --max-parents=0 HEAD 2>/dev/null)
        if test "$target_commit" = "$root_commit"
            set commit_range HEAD
        else
            set commit_range "$target_commit^..HEAD"
        end
    else
        set commit_range "$target_commit..HEAD"
    end

    set -l user_mode ""
    set -l user_name ""
    set -l user_email ""
    set -l user_target ""

    if test $fix_commit_user = true
        set -l user_selection_result (_gish_interactive_user_selection $commit_range)
        if test $status -ne 0
            return 1
        end

        for line in $user_selection_result
            set -l key (echo $line | cut -d: -f1)
            set -l value (echo $line | cut -d: -f2-)

            switch $key
                case mode
                    set user_mode $value
                case name
                    set user_name $value
                case email
                    set user_email $value
            end
        end

        if test "$user_mode" != sync
            set -l target_result (_gish_select_target_user)
            if test $status -ne 0
                return 1
            end
            set user_target (echo $target_result | cut -d: -f2)
        end
    end

    set -l commit_short (git rev-parse --short $target_commit)
    set -l commit_subject (git show -s --format='%s' $target_commit)

    echo "â”Œâ”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€
â”‚ é–‹å§‹ã‚³ãƒŸãƒƒãƒˆï¼š$commit_short
â”‚ ãƒ¡ãƒƒã‚»ãƒ¼ã‚¸ï¼š$commit_subject"

    if test $include_root = true
        echo "â”‚ å‡¦ç†ç¯„å›²ï¼šæŒ‡å®šã‚³ãƒŸãƒƒãƒˆã‹ã‚‰ HEAD ã¾ã§ï¼ˆã‚³ãƒŸãƒƒãƒˆè‡ªä½“ã‚‚å«ã‚€ï¼‰"
    else
        echo "â”‚ å‡¦ç†ç¯„å›²ï¼šæŒ‡å®šã‚³ãƒŸãƒƒãƒˆã®æ¬¡ã‹ã‚‰ HEAD ã¾ã§"
    end

    if test $fix_commit_date = true
        if test -n "$date_str"
            echo "â”‚ æ—¥æ™‚ä¿®æ­£ï¼šå›ºå®šæ—¥æ™‚ ($date_str) ã«è¨­å®š"
        else
            echo "â”‚ æ—¥æ™‚ä¿®æ­£ï¼šCommitterDateã‚’AuthorDateã«åŒæœŸ"
        end
    end

    if test $fix_commit_user = true
        switch $user_mode
            case sync
                echo "â”‚ ä½œæˆè€…ä¿®æ­£ï¼šCommitterã‚’Authorã«çµ±ä¸€"
            case existing new
                echo "â”‚ ä½œæˆè€…ä¿®æ­£ï¼š$user_name <$user_email>"
                switch $user_target
                    case author
                        echo "â”‚ é©ç”¨å¯¾è±¡ï¼šAuthor ã®ã¿"
                    case committer
                        echo "â”‚ é©ç”¨å¯¾è±¡ï¼šCommitter ã®ã¿"
                    case both
                        echo "â”‚ é©ç”¨å¯¾è±¡ï¼šAuthor ã¨ Committer ä¸¡æ–¹"
                end
        end
    end

    echo "â””â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€"

    if test $skip_confirm != true
        read -l -P "ã“ã®è¨­å®šã§ã‚³ãƒŸãƒƒãƒˆæƒ…å ±ã‚’ä¿®æ­£ã—ã¾ã™ã‹ï¼Ÿ (y/N) > " confirm
        if not string match -qr '^[Yy]' -- $confirm
            echo "â„¹ï¸ æ“ä½œã‚’ã‚­ãƒ£ãƒ³ã‚»ãƒ«ã—ã¾ã—ãŸ"
            return 0
        end
    end

    set -l env_filter ""

    set -l date_part ""
    set -l user_part ""

    if test $fix_commit_date = true
        if test -n "$date_str"
            set date_part "export GIT_AUTHOR_DATE=\"$date_str\"; export GIT_COMMITTER_DATE=\"$date_str\";"
        else
            set date_part "export GIT_COMMITTER_DATE=\"\$GIT_AUTHOR_DATE\";"
        end
    end

    if test $fix_commit_user = true
        switch $user_mode
            case sync
                set user_part "export GIT_COMMITTER_NAME=\"\$GIT_AUTHOR_NAME\"; export GIT_COMMITTER_EMAIL=\"\$GIT_AUTHOR_EMAIL\";"
            case existing new
                switch $user_target
                    case author
                        set user_part "export GIT_AUTHOR_NAME=\"$user_name\"; export GIT_AUTHOR_EMAIL=\"$user_email\";"
                    case committer
                        set user_part "export GIT_COMMITTER_NAME=\"$user_name\"; export GIT_COMMITTER_EMAIL=\"$user_email\";"
                    case both
                        set user_part "export GIT_AUTHOR_NAME=\"$user_name\"; export GIT_AUTHOR_EMAIL=\"$user_email\"; export GIT_COMMITTER_NAME=\"$user_name\"; export GIT_COMMITTER_EMAIL=\"$user_email\";"
                end
        end
    end

    set env_filter "$date_part $user_part"

    set -x FILTER_BRANCH_SQUELCH_WARNING 1
    set_color cyan
    echo "âš™ï¸ï¸ ã‚³ãƒŸãƒƒãƒˆæƒ…å ±ã‚’ä¿®æ­£ã—ã¦ã„ã¾ã™"
    set_color normal

    if git filter-branch --force --env-filter "$env_filter" -- $commit_range
        set_color green
        echo "âœ… ã‚³ãƒŸãƒƒãƒˆæƒ…å ±ã®ä¿®æ­£ã«æˆåŠŸã—ã¾ã—ãŸ"
        set_color normal
        set -e FILTER_BRANCH_SQUELCH_WARNING
        return 0
    else
        set_color red
        echo "â›” ã‚³ãƒŸãƒƒãƒˆæƒ…å ±ã®ä¿®æ­£ã«å¤±æ•—ã—ã¾ã—ãŸ"
        set_color normal
        set -e FILTER_BRANCH_SQUELCH_WARNING
        return 1
    end
end
